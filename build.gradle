plugins {
    id "java"
    id 'com.diffplug.spotless' version "6.23.0"
}

group = "com.testcontainers"
version = "1.0.0-SNAPSHOT"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

repositories {
    mavenCentral()
}

ext {
    set("junit5Version", "5.10.0")
    set("testcontainersVersion", "1.19.3")
    set("awsJavaSdkV1Version", "1.12.445")
    set("awsJavaSdkV2Version", "2.20.42")
}

dependencies {
    testImplementation(enforcedPlatform("org.junit:junit-bom:${junit5Version}"))
    testImplementation(enforcedPlatform("org.testcontainers:testcontainers-bom:${testcontainersVersion}"))

    implementation "org.postgresql:postgresql:42.6.0"
    implementation "ch.qos.logback:logback-classic:1.4.8"

    testImplementation "software.amazon.awssdk:s3:${awsJavaSdkV2Version}"
    testImplementation "com.amazonaws:aws-java-sdk-s3:${awsJavaSdkV1Version}"
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:postgresql"
    testImplementation "org.testcontainers:localstack"
}

tasks.named("test") {
    useJUnitPlatform()
    testLogging {
        events = ["PASSED", "FAILED", "SKIPPED"]
        showStandardStreams = true
        exceptionFormat = "full"
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        prettier(['prettier': '3.0.3', 'prettier-plugin-java': '2.3.0'])
            .config([
                'parser': 'java',
                'tabWidth': 2,
                'printWidth': 80,
                'plugins': ['prettier-plugin-java']
            ])
        formatAnnotations()
    }
}